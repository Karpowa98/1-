# 1C
Разработка интеграции.

Функция ПолучитьОбразованияСотрудника(КодСотрудника) Экспорт

	//СотрудникСсылка = Справочники.ВолГУ_ОписаниеСотрудников.НайтиПоКоду(КодСотрудника);
	ПространствоИмен="http://is.volsu.ru/UniversityStaff";
	СотрудникТип = ФабрикаXDTO.Тип(ПространствоИмен, "ОбразованиеСотрудника");
	Сотрудник = ФабрикаXDTO.Создать(СотрудникТип);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Образование.Специальность КАК Специальность,
		|	Образование.СерияДиплома КАК СерияДиплома,
		|	Образование.НомерДиплома КАК НомерДиплома,
		|	Образование.ГодОкончания КАК ГодОкончания,
		|	Образование.Квалификация КАК Квалификация,
		|	Образование.ЛетОбучения КАК ЛетОбучения,
		|	Образование.МесяцевОбучения КАК МесяцевОбучения,
		|	Образование.Файл КАК Файл,
		|	Образование.ТипФайла КАК ТипФайла,
		|	Образование.ВидОбразования.Наименование КАК ВидОбразования,
		|	Образование.УчебноеЗаведение.Наименование КАК УчебноеЗаведение,
		|	Образование.УГС.Наименование КАК УГС
		|ИЗ
		|	Справочник.ВолГУ_ОписаниеСотрудников.Образование КАК Образование
		|ГДЕ
		|	Образование.Ссылка.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", КодСотрудника);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Образование = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "ОбразованиеПолноеОписание"));
		//ЗаполнитьЗначенияСвойств(Образование, Выборка, ,"Файл, УГС");
		Образование.Файл = ВолГУ_WebСервисОбщиеМетоды.ФайлОбъектXDTOПоСсылке(Выборка.Файл);
		Образование.Специальность = Строка(Выборка.Специальность);
		Образование.СерияДиплома = Строка(Выборка.СерияДиплома);
		Образование.НомерДиплома = Строка(Выборка.НомерДиплома);
		Образование.ГодОкончания = Выборка.ГодОкончания;
		Образование.Квалификация = Строка(Выборка.Квалификация);
		Образование.ЛетОбучения = Выборка.ЛетОбучения;
		Образование.МесяцевОбучения = Выборка.МесяцевОбучения;
		Образование.ТипФайла = Строка(Выборка.ТипФайла);
		Образование.ВидОбразования = Строка(Выборка.ВидОбразования);
		Образование.УчебноеЗаведение = Строка(Выборка.УчебноеЗаведение);

		Если Выборка.УГС = Null Тогда
			Образование.УГС = Неопределено;
		Иначе Образование.УГС = Выборка.УГС;
		КонецЕсли;
		Сотрудник.ОбразованиеПолноеОписание.Добавить(Образование);
	КонецЦикла;
	
	Возврат Сотрудник;


КонецФункции 

Функция ПолучитьНаградыСотрудника(КодСотрудника) Экспорт

	//СотрудникСсылка = Справочники.ВолГУ_ОписаниеСотрудников.НайтиПоКоду(КодСотрудника);
	ПространствоИмен="http://is.volsu.ru/UniversityStaff";
	СотрудникТип = ФабрикаXDTO.Тип(ПространствоИмен, "Награды");
	Сотрудник = ФабрикаXDTO.Создать(СотрудникТип);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Награды.НомерПриказа КАК НомерПриказа,
		|	Награды.ДатаПриказа КАК ДатаПриказа,
		|	Награды.ЧейПриказ КАК ЧейПриказ,
		|	Награды.Награда.Наименование КАК Награда
		|ИЗ
		|	Справочник.ВолГУ_ОписаниеСотрудников.Награды КАК Награды
		|ГДЕ
		|	Награды.Ссылка.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", КодСотрудника);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Награда = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "СписокНаград"));
		ЗаполнитьЗначенияСвойств(Награда, Выборка);
		Сотрудник.СписокНаград.Добавить(Награда);
	КонецЦикла;
	
	Возврат Сотрудник;


КонецФункции 

Функция ПолучитьУченуюСтепень(КодСотрудника) Экспорт

	//СотрудникСсылка = Справочники.ВолГУ_ОписаниеСотрудников.НайтиПоКоду(КодСотрудника);
	ПространствоИмен="http://is.volsu.ru/UniversityStaff";
	СотрудникТип = ФабрикаXDTO.Тип(ПространствоИмен, "УченыеСтепени");
	Сотрудник = ФабрикаXDTO.Создать(СотрудникТип);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УченыеСтепени.ДатаПрисужденияУченойСтепени КАК ДатаПрисужденияУченойСтепени,
		|	УченыеСтепени.ДиссертационныйСовет КАК ДиссертационныйСовет,
		|	УченыеСтепени.ДипломСерияНомер КАК ДипломСерияНомер,
		|	УченыеСтепени.ДипломВыданОрганизация КАК ДипломВыданОрганизация,
		|	УченыеСтепени.Файл КАК Файл,
		|	УченыеСтепени.ТипФайла КАК ТипФайла,
		|	УченыеСтепени.УченаяСтепень.Наименование КАК УченаяСтепень,
		|	УченыеСтепени.ОтрасльНауки.Наименование КАК ОтрасльНауки
		|ИЗ
		|	Справочник.ВолГУ_ОписаниеСотрудников.УченыеСтепени КАК УченыеСтепени
		|ГДЕ
		|	УченыеСтепени.Ссылка.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", КодСотрудника);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		УченаяСтепень = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "СписокУченыхСтепеней"));
		ЗаполнитьЗначенияСвойств(УченаяСтепень, Выборка, , "Файл");
		УченаяСтепень.Файл = ВолГУ_WebСервисОбщиеМетоды.ФайлОбъектXDTOПоСсылке(Выборка.Файл);
		Сотрудник.СписокУченыхСтепеней.Добавить(УченаяСтепень);
	КонецЦикла;
	
	Возврат Сотрудник;
КонецФункции 

Функция ПолучитьУченыеЗвания(КодСотрудника) Экспорт

	//СотрудникСсылка = Справочники.ВолГУ_ОписаниеСотрудников.НайтиПоКоду(КодСотрудника);
	ПространствоИмен="http://is.volsu.ru/UniversityStaff";
	СотрудникТип = ФабрикаXDTO.Тип(ПространствоИмен, "УченыеЗвания");
	Сотрудник = ФабрикаXDTO.Создать(СотрудникТип);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УченыеЗвания.ДатаПрисвоенияУченогоЗвания КАК ДатаПрисвоенияУченогоЗвания,
		|	УченыеЗвания.АттестатСерия КАК АттестатСерия,
		|	УченыеЗвания.АттестатНомер КАК АттестатНомер,
		|	УченыеЗвания.АттестатВыданОрганизация КАК АттестатВыданОрганизация,
		|	УченыеЗвания.Файл КАК Файл,
		|	УченыеЗвания.ТипФайла КАК ТипФайла,
		|	УченыеЗвания.УченоеЗвание.Наименование КАК УченоеЗвание,
		|	УченыеЗвания.НаучнаяСпециальность.Наименование КАК НаучнаяСпециальность
		|ИЗ
		|	Справочник.ВолГУ_ОписаниеСотрудников.УченыеЗвания КАК УченыеЗвания
		|ГДЕ
		|	УченыеЗвания.Ссылка.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", КодСотрудника);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		УченыеЗвания = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "СписокУченыхЗваний"));
		ЗаполнитьЗначенияСвойств(УченыеЗвания, Выборка, , "Файл, НаучнаяСпециальность");
		Если Выборка.НаучнаяСпециальность = Null Тогда
			УченыеЗвания.НаучнаяСпециальность = Неопределено;
		Иначе УченыеЗвания.НаучнаяСпециальность = Выборка.НаучнаяСпециальность;
		КонецЕсли;
		УченыеЗвания.Файл = ВолГУ_WebСервисОбщиеМетоды.ФайлОбъектXDTOПоСсылке(Выборка.Файл);
		Сотрудник.СписокУченыхЗваний.Добавить(УченыеЗвания);
	КонецЦикла;
	
	Возврат Сотрудник;
КонецФункции

Функция ПолучитьДисциплины(КодСотрудника) Экспорт

	//СотрудникСсылка = Справочники.ВолГУ_ОписаниеСотрудников.НайтиПоКоду(КодСотрудника);
	ПространствоИмен="http://is.volsu.ru/UniversityStaff";
	СотрудникТип = ФабрикаXDTO.Тип(ПространствоИмен, "ДисциплиныСотрудника");
	Сотрудник = ФабрикаXDTO.Создать(СотрудникТип);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Дисциплины.Дисциплина.Код КАК Код,
		|	Дисциплины.Дисциплина.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ВолГУ_ОписаниеСотрудников.Дисциплины КАК Дисциплины
		|ГДЕ
		|	Дисциплины.Ссылка.Код = &КодСотрудника";
	
	Запрос.УстановитьПараметр("КодСотрудника", КодСотрудника);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Дисциплина = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "Дисциплина"));
		ЗаполнитьЗначенияСвойств(Дисциплина, Выборка);
		Сотрудник.Дисциплина.Добавить(Дисциплина);
	КонецЦикла;
	
	Возврат Сотрудник;
КонецФункции 

Функция ПолучитьСписокПовышенийКвалификации(КодСотрудника) Экспорт

	//СотрудникСсылка = Справочники.ВолГУ_ОписаниеСотрудников.НайтиПоКоду(КодСотрудника);
	ПространствоИмен="http://is.volsu.ru/UniversityStaff";
	СотрудникТип = ФабрикаXDTO.Тип(ПространствоИмен, "ПовышениеКвалификации");
	Сотрудник = ФабрикаXDTO.Создать(СотрудникТип);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПовышениеКвалификации.ВидМероприятияСтр КАК ВидМероприятияСтр,
		|	ПовышениеКвалификации.ВидМероприятия.Наименование КАК ВидМероприятия,
		|	ПовышениеКвалификации.ДатаНачала КАК ДатаНачала,
		|	ПовышениеКвалификации.ДатаОкончания КАК ДатаОкончания,
		|	ПовышениеКвалификации.НаименованиеМероприятия КАК НаименованиеМероприятия,
		|	ПовышениеКвалификации.Организация.Наименование КАК Организация,
		|	ПовышениеКвалификации.СИспользованиемИКТ КАК СИспользованиемИКТ,
		|	ПовышениеКвалификации.ПедагогическийПрофиль КАК ПедагогическийПрофиль,
		|	ПовышениеКвалификации.Стажировка КАК Стажировка,
		|	ПовышениеКвалификации.ТипФайла КАК ТипФайла,
		|	ПовышениеКвалификации.Файл КАК Файл
		|ИЗ
		|	Справочник.ВолГУ_ОписаниеСотрудников.ПовышениеКвалификации КАК ПовышениеКвалификации
		|ГДЕ
		|	ПовышениеКвалификации.Ссылка.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", КодСотрудника);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ПовышениеКвалификации = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "СписокПовышенийКвалификации"));
		ЗаполнитьЗначенияСвойств(ПовышениеКвалификации, Выборка, ,"Файл");
		ПовышениеКвалификации.Файл = ВолГУ_WebСервисОбщиеМетоды.ФайлОбъектXDTOПоСсылке(Выборка.Файл);
		Сотрудник.СписокПовышенийКвалификации.Добавить(ПовышениеКвалификации);
	КонецЦикла;
	
	Возврат Сотрудник;
КонецФункции 

Функция ПолучитьФайлы(КодСотрудника) Экспорт

	ПространствоИмен="http://is.volsu.ru/UniversityStaff";
	СотрудникТип = ФабрикаXDTO.Тип(ПространствоИмен, "Файлы");
	Файлы = ФабрикаXDTO.Создать(СотрудникТип);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВолГУ_ОписаниеСотрудниковФайлы.ТипФайла КАК ТипФайла,
		|	ВолГУ_ОписаниеСотрудниковФайлы.Файл КАК Файл
		|ИЗ
		|	Справочник.ВолГУ_ОписаниеСотрудников.Файлы КАК ВолГУ_ОписаниеСотрудниковФайлы
		|ГДЕ
		|	ВолГУ_ОписаниеСотрудниковФайлы.Ссылка.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", КодСотрудника);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Файл = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "Файл"));
		ЗаполнитьЗначенияСвойств(Файл, Выборка, ,"Файл");
		Файл.Файл = ВолГУ_WebСервисОбщиеМетоды.ФайлОбъектXDTOПоСсылке(Выборка.Файл);
		Файлы.Файл.Добавить(Файл);
	КонецЦикла;
	
	Возврат Файлы;
КонецФункции 

Функция ПолучитьВсюИнформациюОСотруднике(КодСотрудника) Экспорт
	
	ПространствоИмен="http://is.volsu.ru/UniversityStaff";
	СотрудникТип = ФабрикаXDTO.Тип(ПространствоИмен, "ИнформацияПоСотруднику");
	Сотрудник = ФабрикаXDTO.Создать(СотрудникТип);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВолГУ_ОписаниеСотрудников.ТелефонСлужебный КАК ТелефонСлужебный,
		|	ВолГУ_ОписаниеСотрудников.ГрафикПриема КАК ГрафикПриема,
		|	ВолГУ_ОписаниеСотрудников.ВолГУ_GoogleScholar КАК ВолГУ_GoogleScholar,
		|	ВолГУ_ОписаниеСотрудников.ВнешняяОрганизация КАК ВнешняяОрганизация,
		|	ВолГУ_ОписаниеСотрудников.ВнешняяДолжность КАК ВнешняяДолжность,
		|	ВолГУ_ОписаниеСотрудников.СтажОбщий КАК СтажОбщий,
		|	ВолГУ_ОписаниеСотрудников.СтажПоСпециальности КАК СтажПоСпециальности,
		|	ВолГУ_ОписаниеСотрудников.ТематикаНИД КАК ТематикаНИД,
		|	ВолГУ_ОписаниеСотрудников.ТематикаНИДЗаполнено КАК ТематикаНИДЗаполнено,
		|	ВолГУ_ОписаниеСотрудников.Достижения КАК Достижения,
		|	ВолГУ_ОписаниеСотрудников.ДостиженияЗаполнено КАК ДостиженияЗаполнено
		|ИЗ
		|	Справочник.ВолГУ_ОписаниеСотрудников КАК ВолГУ_ОписаниеСотрудников
		|ГДЕ
		|	ВолГУ_ОписаниеСотрудников.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", КодСотрудника);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Выборка.Следующий();
	СтрокаИнформации = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "СписокИнформацииПоСотруднику"));
	ЗаполнитьЗначенияСвойств(СтрокаИнформации, Выборка, ,"Достижения, ТематикаНИД");
	
	СотрудникСсылка = Справочники.ВолГУ_ОписаниеСотрудников.НайтиПоКоду(КодСотрудника);
	Хранилище = Новый ХранилищеЗначения(СотрудникСсылка.Достижения.Получить());
	//Строка = Base64Значение(СотрудникСсылка.Достижения);
	ЗаполнитьФорматированныйДокумент(СотрудникСсылка.Достижения, СтрокаИнформации.Достижения);
	ЗаполнитьФорматированныйДокумент(СотрудникСсылка.ТематикаНИД, СтрокаИнформации.ТематикаНИД);
	
	//СтрокаИнформации.Достижения = Base64Значение(СотрудникСсылка.Достижения);
	//СтрокаИнформации.ТематикаНИД = Base64Значение(СотрудникСсылка.ТематикаНИД);
	Сотрудник.СписокИнформации.Добавить(СтрокаИнформации);
	
	Возврат Сотрудник;
КонецФункции 

Функция ПолучитьПубликацииСотрудника(КодСотрудника) Экспорт

	ПространствоИмен="http://is.volsu.ru/UniversityStaff";
	СотрудникТип = ФабрикаXDTO.Тип(ПространствоИмен, "ПубликацииСотрудника");
	Сотрудник = ФабрикаXDTO.Создать(СотрудникТип);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВолГУ_ПубликацииАвторы.Ссылка.Наименование КАК Наименование,
		|	ВолГУ_ПубликацииАвторы.Ссылка.Год КАК Год,
		|	ВолГУ_ОписаниеСотрудников.Код КАК Код,
		|	ВолГУ_ПубликацииАвторы.Ссылка.ВидПубликации.Наименование КАК ВидПубликации
		|ИЗ
		|	Справочник.ВолГУ_ОписаниеСотрудников КАК ВолГУ_ОписаниеСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВолГУ_Публикации.Авторы КАК ВолГУ_ПубликацииАвторы
		|		ПО ВолГУ_ОписаниеСотрудников.ФизическоеЛицо = ВолГУ_ПубликацииАвторы.Автор
		|ГДЕ
		|	ВолГУ_ОписаниеСотрудников.Код = &Код";
	
	Запрос.УстановитьПараметр("Код", КодСотрудника);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Публикация = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "СписокПубликацийСотрудника"));
		ЗаполнитьЗначенияСвойств(Публикация, Выборка);
		Сотрудник.СписокПубликаций.Добавить(Публикация);
	КонецЦикла;
	
	Возврат Сотрудник;
КонецФункции





